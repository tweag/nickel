use std::collections::HashMap;
use std::ffi::OsString;

use codespan::FileId;

use lalrpop_util::ErrorRecovery;

use super::ExtendedTerm;
use super::utils::*;
use super::lexer::{Token, NormalToken, StringToken, MultiStringToken};

use crate::{mk_app, mk_opn, mk_fun};
use crate::identifier::Ident;
use crate::parser::error::ParseError;
use crate::term::{BinaryOp, RichTerm, Term, UnaryOp, StrChunk, MetaValue,
    MergePriority, Contract, NAryOp, RecordAttrs};
use crate::term::make as mk_term;
use crate::types::{Types, AbsType};

grammar<'input, 'err>(src_id: FileId, errors: &'err mut Vec<ErrorRecovery<usize, Token<'input>, ParseError>>);

WithPos<Rule>: RichTerm = <l: @L> <t: Rule> <r: @R> => t.with_pos(mk_pos(src_id, l, r));
CheckUnbound<Rule>: Types = <l: @L> <t: Rule> <r: @R> =>? check_unbound(&t, mk_span(src_id, l, r)).map_err(|e| lalrpop_util::ParseError::User{error: e}).and(Ok(t));

TypeAnnot: MetaValue = ":" <l: @L> <ty_res: CheckUnbound<Types>> <r: @R> => MetaValue {
    doc: None,
    types: Some(Contract {types: ty_res.clone(), label: mk_label(ty_res, src_id, l, r)}),
    contracts: Vec::new(),
    priority: Default::default(),
    value: None,
};

MetaAnnotAtom: MetaValue = {
    "|" <l: @L> <ty_res: CheckUnbound<Types>> <r: @R> => MetaValue {
        doc: None,
        types: None,
        contracts: vec![Contract {types: ty_res.clone(), label: mk_label(ty_res, src_id, l, r)}],
        priority: Default::default(),
        value: None,
    },
    "|" "default" => MetaValue {
        doc: None,
        types: None,
        contracts: Vec::new(),
        priority: MergePriority::Default,
        value: None
    },
    "|" "doc" <s: StaticString> => MetaValue {
        doc: Some(strip_indent_doc(s)),
        types: None,
        contracts: Vec::new(),
        priority: Default::default(),
        value: None,
    },
};

MetaAnnot: MetaValue = <anns: MetaAnnotAtom+> => anns.into_iter().fold(MetaValue::new(), MetaValue::flatten);

Annot: MetaValue = {
    <TypeAnnot>,
    <ty_ann: TypeAnnot?> <meta: MetaAnnot> => {
        if let Some(ty_meta) = ty_ann {
            MetaValue::flatten(ty_meta, meta)
        }
        else {
            meta
        }
    }
};

pub Term: RichTerm = WithPos<RootTerm>;

pub ExtendedTerm: ExtendedTerm = {
    "let" <id:Ident> <meta: Annot?> "=" <t: Term> => {
        let t = if let Some(mut meta) = meta {
            let pos = t.pos;
            meta.value = Some(t);
            RichTerm::new(Term::MetaValue(meta), pos)
        }
        else {
            t
        };

        ExtendedTerm::ToplevelLet(id, t)
    },
    Term => ExtendedTerm::RichTerm(<>),
}

RootTerm: RichTerm = {
    "let" <id:Ident> <meta: Annot?> "=" <t1: Term> "in"
        <t2: Term> => {
        let t1 = if let Some(mut meta) = meta {
            let pos = t1.pos;
            meta.value = Some(t1);
            RichTerm::new(Term::MetaValue(meta), pos)
        }
        else {
            t1
        };

        mk_term::let_in(id, t1, t2)
    },
    <l: @L> "fun" <ps:Pattern+> "=>" <t: Term> <r: @R> => {
        let pos = mk_pos(src_id, l, r);
        ps.into_iter().rev().fold(t, |t, p| RichTerm {
            term: Box::new(Term::Fun(p, t)),
            pos,
        })
    },
    "switch" "{" <cases: (switch_case ",")*> <last: switch_case?> "}"
        <exp: Term> => {
        let mut acc = HashMap::with_capacity(cases.len());
        let mut default = None;

        for case in cases.into_iter().map(|x| x.0).chain(last.into_iter()) {
            match case {
                SwitchCase::Normal(id, t) => acc.insert(id, t),
                // If there are multiple default cases, the last one silently
                // erases the others. We should have a dedicated error for that
                SwitchCase::Default(t) => default.replace(t),
            };
        }

        RichTerm::from(
            Term::Switch(
                exp,
                acc,
                default,
            )
        )
    },
    "if" <cond: Term> "then" <t1: Term> "else" <t2: Term> =>
        mk_app!(Term::Op1(UnaryOp::Ite(), cond), t1, t2),
    AnnotatedTerm,
    <l: @L> <t: !> <r: @R> => { errors.push(t); RichTerm { term: Box::new(Term::ParseError), pos: mk_pos(src_id, l, r)} },
};

AnnotatedTerm: RichTerm = {
    <t: WithPos<Infix>> <meta: Annot?> => {
        if let Some(mut meta) = meta {
            let pos = t.pos;
            meta.value = Some(t);
            RichTerm::new(Term::MetaValue(meta), pos)
        }
        else {
            t
        }
    },
};

Infix: RichTerm = {
    InfixExpr,
};

Applicative: RichTerm = {
    "import" <s: StaticString> => RichTerm::from(Term::Import(OsString::from(s))),
    <t1:WithPos<Applicative>> <t2: WithPos<RecordOperand>> => mk_app!(t1, t2),
    <op: UOp> <t: WithPos<RecordOperand>> => mk_term::op1(op, t),
    <op: BOpPre> <t1: WithPos<RecordOperand>> <t2: WithPos<Atom>> => mk_term::op2(op, t1, t2),
    NOpPre<WithPos<RecordOperand>>,
    RecordOperand,
};

RecordOperand: RichTerm = {
    Atom,
    RecordOperationChain,
}

RecordOperationChain: RichTerm = {
    <t: WithPos<RecordOperand>> "." <id: Ident> => mk_term::op1(UnaryOp::StaticAccess(id), t),
    <t: WithPos<RecordOperand>> "." <t_id: WithPos<StrChunks>> => mk_term::op2(BinaryOp::DynAccess(), t_id, t),
    <t: WithPos<RecordOperand>> "-$" <t_id: WithPos<Atom>> => mk_term::op2(BinaryOp::DynRemove(), t_id, t),
    <r: WithPos<RecordOperand>> "$[" <id: Term> "=" <t: Term> "]" =>
        mk_app!(mk_term::op2(BinaryOp::DynExtend(), id, r), t),
};

Atom: RichTerm = {
    "(" <CurriedOp> ")",
    "(" <Term> ")",
    "num literal" => RichTerm::from(Term::Num(<>)),
    "null" => RichTerm::from(Term::Null),
    Bool => RichTerm::from(Term::Bool(<>)),
    StrChunks,
    Ident => RichTerm::from(Term::Var(<>)),
    "`" <EnumTag> => RichTerm::from(Term::Enum(<>)),
    "{" <fields: (<RecordField> ",")*> <last: RecordLastField?> "}" => {
        let (last_field, attrs) = match last {
            Some(RecordLastField::Field(f)) => (Some(f), Default::default()),
            Some(RecordLastField::Ellipsis) =>
                (None, RecordAttrs { open: true }),
            None => (None, Default::default())
        };

        let fields = fields.into_iter().chain(last_field.into_iter());
        RichTerm::from(build_record(fields, attrs))
    },
    "[" <terms: (<Term> ",")*> <last: Term?> "]" => {
        let terms : Vec<RichTerm> = terms.into_iter()
            .chain(last.into_iter()).collect();
        RichTerm::from(Term::List(terms))
    }
};

RecordField: (FieldPathElem, RichTerm) = {
    <path: FieldPath> <ty_ann: TypeAnnot?> "=" <t: Term> => {
        let t = if let Some(mut meta) = ty_ann {
            let pos = t.pos;
            meta.value = Some(t);
            RichTerm::new(Term::MetaValue(meta), pos)
        }
        else {
            t
        };

        elaborate_field_path(path, t)
    },
    <path: FieldPath> <ty_ann: TypeAnnot> <meta: MetaAnnot> "=" <t: Term> => {
        let pos = t.pos;
        let mut meta = MetaValue::flatten(ty_ann, meta);
        meta.value = Some(t);
        let t = RichTerm::new(Term::MetaValue(meta), pos);

        elaborate_field_path(path, t)
    },
    <l: @L> <path: FieldPath> <meta: MetaAnnot> <r: @R> <t: ("=" <Term>)?> => {
        let mut meta = meta;
        let pos = t.as_ref()
            .map(|t| t.pos.clone())
            .unwrap_or(mk_pos(src_id, l, r));
        meta.value = t;
        let t = RichTerm::new(Term::MetaValue(meta), pos);
        elaborate_field_path(path, t)
    }
}

RecordLastField: RecordLastField = {
    <RecordField> => RecordLastField::Field(<>),
    ".." => RecordLastField::Ellipsis,
}

FieldPath: Vec<FieldPathElem> = {
    <elems: (<FieldPathElem> ".")*> <last: FieldPathElem> => {
        let mut elems = elems;
        elems.push(last);
        elems
    }
};

FieldPathElem: FieldPathElem = {
    <Ident> => FieldPathElem::Ident(<>),
    <WithPos<StrChunks>> => FieldPathElem::Expr(<>),
};

Pattern: Ident = {
    Ident,
};

Ident: Ident = <l:@L> <i: "identifier"> <r:@R> => Ident(i.to_string(), mk_pos(src_id, l, r));

Bool: bool = {
    "true" => true,
    "false" => false,
};

StrChunks : RichTerm = {
  <start: StringStart> <fst: ChunkLiteral?> <chunks: (ChunkExpr+ChunkLiteral)*> <lasts:ChunkExpr*> <end: StringEnd> => {
        debug_assert_eq!(start, end);

        let chunks: Vec<StrChunk<RichTerm>> = fst.into_iter()
            .map(StrChunk::Literal)
            .chain(chunks.into_iter()
                .map(|(mut es, s)| {
                    es.push(StrChunk::Literal(s));
                    es
                })
                .flatten())
            .chain(lasts.into_iter())
            .collect();

        let mut chunks = if start == StringKind::Multiline {
            strip_indent(chunks)
        }
        else {
            chunks
        };
        chunks.reverse();

        RichTerm::from(Term::StrChunks(chunks))
    },
};

StringStart : StringKind = {
    "\"" => StringKind::Standard,
    "m#\"" => StringKind::Multiline,
};

StringEnd : StringKind = {
    "\"" => StringKind::Standard,
    "\"#m" => StringKind::Multiline,
};

ChunkLiteral : String =
    <parts: ChunkLiteralPart+> => {
        parts.into_iter().fold(String::new(), |mut acc, part| {
            match part {
                ChunkLiteralPart::Str(s) => acc.push_str(s),
                ChunkLiteralPart::Char(c) => acc.push(c),
            };

            acc
        })
    };

ChunkExpr: StrChunk<RichTerm> = HashBrace <t: WithPos<Term>> "}" => StrChunk::Expr(t, 0);

HashBrace = { "#{", "multstr #{" };

StaticString: String = StringStart <s: ChunkLiteral> StringEnd => s;

EnumTag: Ident = {
    <Ident>,
    <StaticString> => <>.into(),
}


ChunkLiteralPart: ChunkLiteralPart<'input> = {
    "str literal" => ChunkLiteralPart::Str(<>),
    "multstr literal" => ChunkLiteralPart::Str(<>),
    "str esc char" => ChunkLiteralPart::Char(<>),
    };

UOp: UnaryOp = {
    "isNum" => UnaryOp::IsNum(),
    "isBool" => UnaryOp::IsBool(),
    "isStr" => UnaryOp::IsStr(),
    "isFun" => UnaryOp::IsFun(),
    "isList" => UnaryOp::IsList(),
    "isRecord" => UnaryOp::IsRecord(),
    "blame" => UnaryOp::Blame(),
    "chngPol" => UnaryOp::ChangePolarity(),
    "polarity" => UnaryOp::Pol(),
    "goDom" => UnaryOp::GoDom(),
    "goCodom" => UnaryOp::GoCodom(),
    "goList" => UnaryOp::GoList(),
    "wrap" => UnaryOp::Wrap(),
    "embed" <Ident> => UnaryOp::Embed(<>),
    "map"  => UnaryOp::ListMap(),
    "generate" => UnaryOp::ListGen(),
    "recordMap" => UnaryOp::RecordMap(),
    "seq" => UnaryOp::Seq(),
    "deepSeq" => UnaryOp::DeepSeq(),
    "head" => UnaryOp::ListHead(),
    "tail" => UnaryOp::ListTail(),
    "length" => UnaryOp::ListLength(),
    "fieldsOf" => UnaryOp::FieldsOf(),
    "valuesOf" => UnaryOp::ValuesOf(),
    "strTrim" => UnaryOp::StrTrim(),
    "strChars" => UnaryOp::StrChars(),
    "charCode" => UnaryOp::CharCode(),
    "charFromCode" => UnaryOp::CharFromCode(),
    "strUppercase" => UnaryOp::StrUppercase(),
    "strLowercase" => UnaryOp::StrLowercase(),
    "strLength" => UnaryOp::StrLength(),
    "strFrom" => UnaryOp::ToStr(),
    "numFrom" => UnaryOp::NumFromStr(),
    "enumFrom" => UnaryOp::EnumFromStr(),
};

switch_case: SwitchCase = {
    "`" <id: EnumTag> "=>" <t: Term> => SwitchCase::Normal(id, t),
    "_" "=>" <t: Term> => SwitchCase::Default(<>),
}

InfixBOp2: BinaryOp = {
    "++" => BinaryOp::StrConcat(),
    "@" => BinaryOp::ListConcat(),
}

InfixBOp3: BinaryOp = {
    "*" => BinaryOp::Mult(),
    "/" => BinaryOp::Div(),
    "%" => BinaryOp::Modulo(),
}

InfixBOp4: BinaryOp = {
    "+" => BinaryOp::Plus(),
    "-" => BinaryOp::Sub(),
}

InfixUOp5: UnaryOp = {
    "!" => UnaryOp::BoolNot(),
}

InfixBOp6: BinaryOp = {
    "&" => BinaryOp::Merge(),
}

InfixBOp7: BinaryOp = {
    "<" => BinaryOp::LessThan(),
    "<=" => BinaryOp::LessOrEq(),
    ">" => BinaryOp::GreaterThan(),
    ">=" => BinaryOp::GreaterOrEq(),
}

InfixBOp8: BinaryOp = {
    "==" => BinaryOp::Eq(),
}

InfixLazyBOp9: UnaryOp = {
    "&&" => UnaryOp::BoolAnd(),
}

InfixLazyBOp10: UnaryOp = {
    "||" => UnaryOp::BoolOr(),
}

InfixBOp: BinaryOp = {
    InfixBOp2,
    InfixBOp3,
    InfixBOp4,
    InfixBOp6,
    InfixBOp7,
    InfixBOp8,
}

InfixLazyBOp: UnaryOp = {
    InfixUOp5,
    InfixLazyBOp9,
    InfixLazyBOp10,
}

InfixOp: InfixOp = {
    <InfixBOp> => <>.into(),
    <InfixLazyBOp> => <>.into(),
}

CurriedOp: RichTerm = {
    <l: @L> <op: InfixOp> <r: @R> => op.eta_expand(mk_pos(src_id, l, r)),
    <l: @L> "|>" <r: @R> =>
        mk_fun!("x1", "x2",
            mk_app!(mk_term::var("x2"), mk_term::var("x1"))
            .with_pos(mk_pos(src_id, l, r))
        ),
    <l: @L> "!=" <r: @R> =>
        mk_fun!("x1", "x2",
            mk_term::op1(
                UnaryOp::BoolNot(),
                Term::Op2(BinaryOp::Eq(),
                    mk_term::var("x2"),
                    mk_term::var("x1")
                )
            )
            .with_pos(mk_pos(src_id, l, r))
        ),
}

InfixUOpApp<UOp, Expr>: RichTerm =
  <op: UOp> <t: WithPos<Expr>> => mk_term::op1(op, t);

InfixBOpApp<BOp, LExpr, RExpr>: RichTerm =
  <t1: WithPos<LExpr>> <op: BOp> <t2: WithPos<RExpr>> => mk_term::op2(op, t1, t2);

InfixLazyBOpApp<UOp, LExpr, RExpr>: RichTerm =
  <t1: WithPos<LExpr>> <op: UOp> <t2: WithPos<RExpr>> =>
    mk_app!(mk_term::op1(op, t1), t2);

InfixExpr: RichTerm = {
    #[precedence(level="0")]
    Applicative,

    #[precedence(level="1")]
    "-" <WithPos<InfixExpr>> => mk_term::op2(BinaryOp::Sub(), Term::Num(0.0), <>),

    #[precedence(level="2")] #[assoc(side="left")]
    InfixBOpApp<InfixBOp2, InfixExpr, InfixExpr>,

    #[precedence(level="3")] #[assoc(side="left")]
    InfixBOpApp<InfixBOp3, InfixExpr, InfixExpr>,

    #[precedence(level="4")] #[assoc(side="left")]
    InfixBOpApp<InfixBOp4, InfixExpr, InfixExpr>,

    #[precedence(level="5")]
    InfixUOpApp<InfixUOp5, InfixExpr>,

    #[precedence(level="6")] #[assoc(side="left")]
    InfixBOpApp<InfixBOp6, InfixExpr, InfixExpr>,
    <t1: WithPos<InfixExpr>> "|>" <t2: WithPos<InfixExpr>> => mk_app!(t2, t1),

    #[precedence(level="7")] #[assoc(side="left")]
    InfixBOpApp<InfixBOp7, InfixExpr, InfixExpr>,

    #[precedence(level="8")] #[assoc(side="left")]
    InfixBOpApp<InfixBOp8, InfixExpr, InfixExpr>,
    <t1: WithPos<InfixExpr>> "!=" <t2: WithPos<InfixExpr>> =>
        mk_term::op1(UnaryOp::BoolNot(), Term::Op2(BinaryOp::Eq(), t1, t2)),

    #[precedence(level="9")] #[assoc(side="left")]
    InfixLazyBOpApp<InfixLazyBOp9, InfixExpr, InfixExpr>,

    #[precedence(level="10")] #[assoc(side="left")]
    InfixLazyBOpApp<InfixLazyBOp10, InfixExpr, InfixExpr>,
}

BOpPre: BinaryOp = {
    "unwrap" => BinaryOp::Unwrap(),
    "goField" => BinaryOp::GoField(),
    "hasField" => BinaryOp::HasField(),
    "elemAt" => BinaryOp::ListElemAt(),
    "tag" => BinaryOp::Tag(),
    "hash" => BinaryOp::Hash(),
    "serialize" => BinaryOp::Serialize(),
    "deserialize" => BinaryOp::Deserialize(),
    "pow" => BinaryOp::Pow(),
    "strSplit" => BinaryOp::StrSplit(),
    "strContains" => BinaryOp::StrContains(),
    "strMatch" => BinaryOp::StrMatch(),
    "strIsMatch" => BinaryOp::StrIsMatch(),
}

NOpPre<ArgRule>: RichTerm = {
    "strReplace" <t1: ArgRule> <t2: ArgRule> <t3: ArgRule> =>
        mk_opn!(NAryOp::StrReplace(), t1, t2, t3),
    "strReplaceRegex" <t1: ArgRule> <t2: ArgRule> <t3: ArgRule> =>
        mk_opn!(NAryOp::StrReplaceRegex(), t1, t2, t3),
    "strSubstr" <t1: ArgRule> <t2: ArgRule> <t3: ArgRule> =>
        mk_opn!(NAryOp::StrSubstr(), t1, t2, t3),
}

Types: Types = {
    "forall" <ids: Ident+> "." <ty: Arrows> =>
        ids.into_iter().rev().fold(
            ty,
            |acc, id| Types(AbsType::Forall(id, Box::new(acc)))
        ),
    <Arrows>
}

Arrows: Types = {
    <s: subType> "->" <t:Arrows> => Types(AbsType::Arrow(Box::new(s), Box::new(t))),
    <subType>,
};

BaseType: Types = {
    "Dyn" => Types(AbsType::Dyn()),
    "Num" => Types(AbsType::Num()),
    "Bool" => Types(AbsType::Bool()),
    "Str" => Types(AbsType::Str()),
};

RowTail: Types = {
    <Ident> => Types(AbsType::Var(<>)),
    "Dyn" => Types(AbsType::Dyn()),
}

subType : Types = {
    <BaseType>,
    "List" <ty: subType?> => {
        let ty = Box::new(ty.unwrap_or(Types(AbsType::Dyn())));
        Types(AbsType::List(ty))
    },
    <Ident> => Types(AbsType::Var(<>)),
    "#" <WithPos<RecordOperand>> => Types(AbsType::Flat(<>)),
    "(" <Types> ")",
    "<" <rows:(<EnumTag> ",")*> <last: (<EnumTag>)?> <tail: ("|" <EnumTag>)?> ">" => {
        let ty = rows.into_iter()
            .chain(last.into_iter())
            // As we build row types as a linked list via a fold on the original
            // iterator, the order of identifiers is reversed. This not a big deal
            // but it's less confusing to the user to print them in the original
            // order for error reporting.
            .rev()
            .fold(
                Types(
                    match tail {
                        Some(id) => AbsType::Var(id),
                        None => AbsType::RowEmpty(),
                    }
                ),
                |t, i| Types(AbsType::RowExtend(i, None, Box::new(t)))
            );
        Types(AbsType::Enum(Box::new(ty)))
    },
    "{" <rows:(<Ident> ":" <Types> ",")*>
        <last:(<Ident> ":" <Types>)?>
        <tail: ("|" <RowTail>)?> "}" => {
        let ty = rows.into_iter()
            .chain(last.into_iter())
            // As we build row types as a linked list via a fold on the original
            // iterator, the order of identifiers is reversed. This not a big deal
            // but it's less confusing to the user to print them in the original
            // order for error reporting.
            .rev()
            .fold(
                tail.unwrap_or(Types(AbsType::RowEmpty())),
                |t, i_ty| {
                    let (i, ty) = i_ty;
                    Types(AbsType::RowExtend(i, Some(Box::new(ty)), Box::new(t)))
                }
            );
        Types(AbsType::StaticRecord(Box::new(ty)))
    },
    "{" "_" ":" <Types> "}" => Types(AbsType::DynRecord(Box::new(<>))),
};

extern {
    type Location = usize;
    type Error = ParseError;

    enum Token<'input> {
        "identifier" => Token::Normal(NormalToken::Identifier(<&'input str>)),
        "str literal" => Token::Str(StringToken::Literal(<&'input str>)),
        "str esc char" => Token::Str(StringToken::EscapedChar(<char>)),
        "multstr literal" => Token::MultiStr(MultiStringToken::Literal(<&'input str>)),
        "num literal" => Token::Normal(NormalToken::NumLiteral(<f64>)),

        "if" => Token::Normal(NormalToken::If),
        "then" => Token::Normal(NormalToken::Then),
        "else" => Token::Normal(NormalToken::Else),
        "forall" => Token::Normal(NormalToken::Forall),
        "in" => Token::Normal(NormalToken::In),
        "let" => Token::Normal(NormalToken::Let),
        "switch" => Token::Normal(NormalToken::Switch),

        "null" => Token::Normal(NormalToken::Null),
        "true" => Token::Normal(NormalToken::True),
        "false" => Token::Normal(NormalToken::False),

        "," => Token::Normal(NormalToken::Comma),
        ":" => Token::Normal(NormalToken::Colon),
        "$" => Token::Normal(NormalToken::Dollar),
        "=" => Token::Normal(NormalToken::Equals),
        "!=" => Token::Normal(NormalToken::NotEquals),
        "&" => Token::Normal(NormalToken::Ampersand),
        "." => Token::Normal(NormalToken::Dot),
        "$[" => Token::Normal(NormalToken::DollarBracket),
        "#{" => Token::Str(StringToken::HashBrace),
        "multstr #{" => Token::MultiStr(MultiStringToken::Interpolation),
        "-$" => Token::Normal(NormalToken::MinusDollar),

        "+" => Token::Normal(NormalToken::Plus),
        "-" => Token::Normal(NormalToken::Minus),
        "*" => Token::Normal(NormalToken::Times),
        "/" => Token::Normal(NormalToken::Div),
        "%" => Token::Normal(NormalToken::Percent),
        "++" => Token::Normal(NormalToken::DoublePlus),
        "==" => Token::Normal(NormalToken::DoubleEq),
        "@" => Token::Normal(NormalToken::At),
        "&&" => Token::Normal(NormalToken::DoubleAnd),
        "||" => Token::Normal(NormalToken::DoublePipe),
        "!" => Token::Normal(NormalToken::Bang),
        ".." => Token::Normal(NormalToken::Ellipsis),

        "fun" => Token::Normal(NormalToken::Fun),
        "import" => Token::Normal(NormalToken::Import),
        "|" => Token::Normal(NormalToken::Pipe),
        "|>" => Token::Normal(NormalToken::RightPipe),
        "->" => Token::Normal(NormalToken::SimpleArrow),
        "=>" => Token::Normal(NormalToken::DoubleArrow),
        "#" => Token::Normal(NormalToken::Hash),
        "`" => Token::Normal(NormalToken::Backtick),
        "_" => Token::Normal(NormalToken::Underscore),
        "\"" => Token::Normal(NormalToken::DoubleQuote),
        "\"#m" => Token::MultiStr(MultiStringToken::End),
        "m#\"" => Token::Normal(NormalToken::MultiStringStart(<usize>)),

        "Num" => Token::Normal(NormalToken::Num),
        "Dyn" => Token::Normal(NormalToken::Dyn),
        "Str" => Token::Normal(NormalToken::Str),
        "Bool" => Token::Normal(NormalToken::Bool),
        "List" => Token::Normal(NormalToken::List),

        "tag" => Token::Normal(NormalToken::Tag),
        "isNum" => Token::Normal(NormalToken::IsNum),
        "isBool" => Token::Normal(NormalToken::IsBool),
        "isStr" => Token::Normal(NormalToken::IsStr),
        "isFun" => Token::Normal(NormalToken::IsFun),
        "isList" => Token::Normal(NormalToken::IsList),
        "isRecord" => Token::Normal(NormalToken::IsRecord),
        "blame" => Token::Normal(NormalToken::Blame),
        "chngPol" => Token::Normal(NormalToken::ChangePol),
        "polarity" => Token::Normal(NormalToken::Polarity),
        "goDom" => Token::Normal(NormalToken::GoDom),
        "goCodom" => Token::Normal(NormalToken::GoCodom),
        "goList" => Token::Normal(NormalToken::GoList),
        "goField" => Token::Normal(NormalToken::GoField),
        "wrap" => Token::Normal(NormalToken::Wrap),
        "unwrap" => Token::Normal(NormalToken::Unwrap),
        "embed" => Token::Normal(NormalToken::Embed),
        "recordMap" => Token::Normal(NormalToken::RecordMap),
        "seq" => Token::Normal(NormalToken::Seq),
        "deepSeq" => Token::Normal(NormalToken::DeepSeq),
        "head" => Token::Normal(NormalToken::Head),
        "tail" => Token::Normal(NormalToken::Tail),
        "length" => Token::Normal(NormalToken::Length),
        "fieldsOf" => Token::Normal(NormalToken::FieldsOf),
        "valuesOf" => Token::Normal(NormalToken::ValuesOf),
        "pow" => Token::Normal(NormalToken::Pow),

        "hasField" => Token::Normal(NormalToken::HasField),
        "map" => Token::Normal(NormalToken::Map),
        "generate" => Token::Normal(NormalToken::ListGen),
        "elemAt" => Token::Normal(NormalToken::ElemAt),
        "merge" => Token::Normal(NormalToken::Merge),
        "default" => Token::Normal(NormalToken::Default),
        "doc" => Token::Normal(NormalToken::Doc),

        "hash" => Token::Normal(NormalToken::OpHash),
        "serialize" => Token::Normal(NormalToken::Serialize),
        "deserialize" => Token::Normal(NormalToken::Deserialize),
        "strSplit" => Token::Normal(NormalToken::StrSplit),
        "strTrim" => Token::Normal(NormalToken::StrTrim),
        "strChars" => Token::Normal(NormalToken::StrChars),
        "charCode" => Token::Normal(NormalToken::CharCode),
        "charFromCode" => Token::Normal(NormalToken::CharFromCode),
        "strUppercase" => Token::Normal(NormalToken::StrUppercase),
        "strLowercase" => Token::Normal(NormalToken::StrLowercase),
        "strContains" => Token::Normal(NormalToken::StrContains),
        "strReplace" => Token::Normal(NormalToken::StrReplace),
        "strReplaceRegex" => Token::Normal(NormalToken::StrReplaceRegex),
        "strIsMatch" => Token::Normal(NormalToken::StrIsMatch),
        "strMatch" => Token::Normal(NormalToken::StrMatch),
        "strLength" => Token::Normal(NormalToken::StrLength),
        "strSubstr" => Token::Normal(NormalToken::StrSubstr),
        "strFrom" => Token::Normal(NormalToken::ToStr),
        "numFrom" => Token::Normal(NormalToken::NumFromStr),
        "enumFrom" => Token::Normal(NormalToken::EnumFromStr),

        "{" => Token::Normal(NormalToken::LBrace),
        "}" => Token::Normal(NormalToken::RBrace),
        "[" => Token::Normal(NormalToken::LBracket),
        "]" => Token::Normal(NormalToken::RBracket),
        "(" => Token::Normal(NormalToken::LParen),
        ")" => Token::Normal(NormalToken::RParen),
        "<" => Token::Normal(NormalToken::LAngleBracket),
        "<=" => Token::Normal(NormalToken::LessOrEq),
        ">" => Token::Normal(NormalToken::RAngleBracket),
        ">=" => Token::Normal(NormalToken::GreaterOrEq),
    }
}
