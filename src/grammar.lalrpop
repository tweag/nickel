use crate::identifier::Ident;
use crate::term::{BinaryOp, RichTerm, Term, UnaryOp, StrChunk};
use crate::types::{Types, AbsType};
use super::utils::{mk_span, mk_label};
use super::lexer::{Token, LexicalError};
use std::collections::HashMap;
use either::*;
use codespan::FileId;

grammar<'input>(src_id: FileId);

pub Term: RichTerm = SpTerm<RichTerm>;

SpTerm<Rule>: RichTerm =
    <l: @L> <t: Rule> <r: @R> => match t {
        RichTerm {term: t, pos: _} => RichTerm {
            term: t,
            pos: Some(mk_span(src_id, l, r))
        }
    };

RichTerm: RichTerm = {
    <l: @L> "fun" <ps:Pattern+> "=>" <t: SpTerm<Term>> <r: @R> => {
        let pos = Some(mk_span(src_id, l, r));
        ps.into_iter().rev().fold(t, |t, p| RichTerm {
            term: Box::new(Term::Fun(p, t)),
            pos: pos.clone()
        })
    },
    "let" <id:Ident> "=" <t1:SpTerm<Term>> "in" <t2:SpTerm<Term>> =>
        RichTerm::new(Term::Let(id, t1, t2)),
    "if" <b:SpTerm<Term>> "then" <t:SpTerm<Term>> "else" <e:SpTerm<Term>> =>
        RichTerm::app(RichTerm::app(RichTerm::new(Term::Op1(UnaryOp::Ite(), b)), t), e),
    "import" <s: Str> => RichTerm::new(Term::Import(s)),
    SpTerm<Operation>
};

Operation: RichTerm = {
    <t1: SpTerm< Applicative>> <op: BOpIn> <t2: SpTerm<Operation>> => RichTerm::new(Term::Op2(op, t1, t2)),
    SpTerm< Applicative>,
};

Applicative: RichTerm = {
    <t1:SpTerm< Applicative>> <t2: SpTerm<Atom>> => RichTerm::new(Term::App(t1, t2)),
    <op: UOp> <t: SpTerm<Atom>> => RichTerm::new(Term::Op1(op, t)),
    <op: BOpPre> <t1: SpTerm<Atom>> <t2: SpTerm<Atom>> => RichTerm::new(Term::Op2(op, t1, t2)),
    SpTerm<RecordOperationChain>,
    SpTerm<Atom>,
};

RecordOperand: RichTerm = {
    SpTerm<Atom>,
    SpTerm<RecordOperationChain>,
}

RecordOperationChain: RichTerm = {
    <t: SpTerm<RecordOperand>> "." <id: Ident> => RichTerm::new(Term::Op1(UnaryOp::StaticAccess(id), t)),
    <t: SpTerm<RecordOperand>> ".$" <t_id: SpTerm<Atom>> => RichTerm::new(Term::Op2(BinaryOp::DynAccess(), t_id, t)),
    <t: SpTerm<RecordOperand>> "-$" <t_id: SpTerm<Atom>> => RichTerm::new(Term::Op2(BinaryOp::DynRemove(), t_id, t)),
    <r: SpTerm<RecordOperand>> "$[" <id: SpTerm<Term>> "=" <t: SpTerm<Term>> "]" =>
        RichTerm::new(Term::Op2(BinaryOp::DynExtend(t), id, r)),
};

Atom: RichTerm = {
    "(" <SpTerm<Term>> ")",
    <l: @L> "Promise(" <ty: Types> "," <t: SpTerm<Term>> ")" <r: @R> =>
        RichTerm::new(Term::Promise(ty.clone(), mk_label(ty, src_id, l, r), t)),
    <l: @L> "Assume(" <ty: Types> "," <t: SpTerm<Term>> ")" <r: @R> =>
        RichTerm::new(Term::Assume(ty.clone(), mk_label(ty, src_id, l, r), t)),
    <l: @L> "Contract(" <ty: Types> ")" <r: @R> =>
        RichTerm::new(Term::Contract(ty.clone(), mk_label(ty, src_id, l, r))),
    "Default(" <t: SpTerm<Term>> ")" => RichTerm::new(Term::DefaultValue(t)),
    <l: @L> "ContractDefault(" <ty: Types> "," <t: SpTerm<Term>> ")" <r: @R> =>
        RichTerm::new(Term::ContractWithDefault(ty.clone(),
            mk_label(ty, src_id, l, r), t)
        ),
    "Docstring(" <s: Str> "," <t: SpTerm<Term>> ")" => RichTerm::new(Term::Docstring(s, t)),
    Num => RichTerm::new(Term::Num(<>)),
    Bool => RichTerm::new(Term::Bool(<>)),
    <StrChunks>,
    Ident => RichTerm::new(Term::Var(<>)),
    "`" <Ident> => RichTerm::new(Term::Enum(<>)),
    "{" <fields: (RecordField ";")*> <last: RecordField?> "}" => {
        let mut static_map = HashMap::new();
        let mut dynamic_fields = Vec::new();

        fields
            .into_iter()
            .map(|x| x.0)
            .chain(last.into_iter())
            .for_each(|field| match field {
                Left((id, t)) => { static_map.insert(id, t) ;}
                Right(t) => dynamic_fields.push(t),
            });

        let static_rec = RichTerm::new(Term::RecRecord(static_map));

        dynamic_fields.into_iter().fold(static_rec, |rec, field| {
            let (id_t, t) = field;
            RichTerm::new(Term::Op2(BinaryOp::DynExtend(t), id_t, rec))
        })
    },
    "[" <terms: (SpTerm<Atom> ",")*> <last: SpTerm<Term>?> "]" => {
        let terms : Vec<RichTerm> = terms.into_iter()
            .map(|x| x.0)
            .chain(last.into_iter()).collect();
        RichTerm::new(Term::List(terms))
    }
};

RecordField: Either<(Ident, RichTerm), (RichTerm, RichTerm)> = {
    <id: Ident> "=" <t: SpTerm<Term>> =>
        Either::Left((id, t)),
    "$" <id: SpTerm<Term>> "=" <t: SpTerm<Term>> =>
        Either::Right((id, t)),
}

Pattern: Ident = {
    Ident,
};

Ident: Ident = "identifier" => Ident(<>.to_string());

Num: f64 = "num literal" => <>;

Bool: bool = {
    "true" => true,
    "false" => false,
};

StrChunks : RichTerm = "\"" <chunks: StrChunk+> "\"" => {
    // We transform generic string chunks to a simple string literal if
    // possible, as this should be a fairly common case, and evaluating a string
    // as chunks do incur a small overhead.
    if chunks.len() == 1 {
        match chunks.into_iter().next().unwrap() {
            StrChunk::Literal(s) => RichTerm::new(Term::Str(s)),
            chunk => RichTerm::new(Term::StrChunks(vec![chunk])),
        }
    }
    else {
        // Chunks are stored in reverse order (see the comment at the definition
        // of Term::StrChunks).
        // LALRPOP does not let us declare chunks as mutable, so we move it
        // first.
        let mut chunks_mut = chunks;
        chunks_mut.reverse();
        RichTerm::new(Term::StrChunks(chunks_mut))
    }
};

StrChunk : StrChunk<RichTerm> = {
    "str literal" => StrChunk::Literal(<>),
    "${" <t: SpTerm<RichTerm>> "}" => StrChunk::Expr(t),
};

Str: String = "\"" <s: "str literal"> "\"" => s;

UOp: UnaryOp<RichTerm> = {
    "isZero" => UnaryOp::IsZero(),
    "isNum" => UnaryOp::IsNum(),
    "isBool" => UnaryOp::IsBool(),
    "isStr" => UnaryOp::IsStr(),
    "isFun" => UnaryOp::IsFun(),
    "isList" => UnaryOp::IsList(),
    "isRecord" => UnaryOp::IsRecord(),
    "blame" => UnaryOp::Blame(),
    "chngPol" => UnaryOp::ChangePolarity(),
    "polarity" => UnaryOp::Pol(),
    "goDom" => UnaryOp::GoDom(),
    "goCodom" => UnaryOp::GoCodom(),
    "tag" <s: Str> => UnaryOp::Tag(s),
    "wrap" => UnaryOp::Wrap(),
    "embed" <Ident> => UnaryOp::Embed(<>),
    "switch" "{" <ds: (switch_case ",")*> <default: switch_default?> "}" =>
        UnaryOp::Switch(
            ds.into_iter().map(|x| x.0).collect(),
            default,
        ),
    "mapRec" <Atom> => UnaryOp::MapRec(<>),
    "seq" => UnaryOp::Seq(),
    "deepSeq" => UnaryOp::DeepSeq(),
    "head" => UnaryOp::ListHead(),
    "tail" => UnaryOp::ListTail(),
    "length" => UnaryOp::ListLength(),
    "fieldsOf" => UnaryOp::FieldsOf(),
};

switch_case: (Ident, RichTerm) = {
    <id: Ident> "=>" <t: SpTerm<Atom> > => (id, t),
}

switch_default: RichTerm = {
    "_" "=>" <SpTerm<Atom>> "," => <>,
}

BOpIn: BinaryOp<RichTerm> = {
    "operator" => {
        match <> {
            "+" => BinaryOp::Plus(),
            "++" => BinaryOp::PlusStr(),
            "==" => BinaryOp::Eq(),
            "@" => BinaryOp::ListConcat(),
            op => panic!("Unkown operator {}", op)
        }
    },
};

BOpPre: BinaryOp<RichTerm> = {
    "unwrap" => BinaryOp::Unwrap(),
    "hasField" => BinaryOp::HasField(),
    "map" => BinaryOp::ListMap(),
    "elemAt" => BinaryOp::ListElemAt(),
    "merge" => BinaryOp::Merge(),
}

Types: Types = {
    "forall" <id: Ident> "." <ty: Arrows> => Types(AbsType::Forall(id, Box::new(ty))),
    <Arrows>
}

Arrows: Types = {
    <s: subType> "->" <t:Arrows> => Types(AbsType::Arrow(Box::new(s), Box::new(t))),
    <subType>,
};

BaseType: Types = {
    "type" => {
        match <> {
            "Dyn" => Types(AbsType::Dyn()),
            "Num" => Types(AbsType::Num()),
            "Bool" => Types(AbsType::Bool()),
            "Str" => Types(AbsType::Str()),
            "List" => Types(AbsType::List()),
            _ => panic!()
        }
    }
}

subType : Types = {
    <BaseType>,
    <Ident> => Types(AbsType::Var(<>)),
    "#" <SpTerm<RichTerm>> => Types(AbsType::Flat(<>)),
    "(" <Types> ")" => <>,
    "(" "|" <r:(<Ident> ",")*> <last: (<Ident>)?>"|" <rest: Ident?> ")" =>
        r.into_iter()
            .chain(last.into_iter())
            // As we build row types as a linked list via a fold on the original
            // iterator, the order of identifiers is reversed. This not a big deal
            // but it's less confusing to the user to print them in the original
            // order for error reporting.
            .rev()
            .fold(
                Types(
                    match rest {
                        Some(id) => AbsType::Var(id),
                        None => AbsType::RowEmpty(),
                    }
                ),
                |t, i| Types(AbsType::RowExtend(i, None, Box::new(t)))
            ),
    "{" "|" <r:(<Ident> ":" <Types> ",")*> <last:(<Ident> ":" <Types>)?> "|"
    <rest: Ident?> "}" =>
        r.into_iter()
            .chain(last.into_iter())
            // As we build row types as a linked list via a fold on the original
            // iterator, the order of identifiers is reversed. This not a big deal
            // but it's less confusing to the user to print them in the original
            // order for error reporting.
            .rev()
            .fold(
                Types(
                    match rest {
                        Some(id) => AbsType::Var(id),
                        None => AbsType::RowEmpty(),
                    }
                ),
                |t, i_ty| {
                    let (i, ty) = i_ty;
                    Types(AbsType::RowExtend(i, Some(Box::new(ty)), Box::new(t)))
                }
            ),
    "<" <subType> ">" => Types(AbsType::Enum(Box::new(<>))),
    "{" <subType> "}" => Types(AbsType::StaticRecord(Box::new(<>))),
    "{" "_" ":" <Types> "}" => Types(AbsType::DynRecord(Box::new(<>))),
};

extern {
    type Location = usize;
    type Error = LexicalError;

    enum Token<'input> {
        "identifier" => Token::Identifier(<&'input str>),
        "operator" => Token::BinaryOp(<&'input str>),
        "type" => Token::Type(<&'input str>),
        "str literal" => Token::StrLiteral(<String>),
        "num literal" => Token::NumLiteral(<f64>),

        "if" => Token::If,
        "then" => Token::Then,
        "else" => Token::Else,
        "forall" => Token::Forall,
        "in" => Token::In,
        "let" => Token::Let,
        "switch" => Token::Switch,

        "true" => Token::True,
        "false" => Token::False,

        "," => Token::Comma,
        ":" => Token::Colon,
        "$" => Token::Dollar,
        "=" => Token::Equals,
        ";" => Token::SemiCol,
        "." => Token::Dot,
        ".$" => Token::DotDollar,
        "$[" => Token::DollarBracket,
        "${" => Token::DollarBrace,
        "-$" => Token::MinusDollar,
        "$=" => Token::DollarEquals,
        "fun" => Token::Fun,
        "import" => Token::Import,
        "|" => Token::Pipe,
        "->" => Token::SimpleArrow,
        "=>" => Token::DoubleArrow,
        "#" => Token::Hash,
        "`" => Token::Backtick,
        "_" => Token::Underscore,
        "\"" => Token::DoubleQuote,

        "tag" => Token::Tag,
        "Assume(" => Token::Assume,
        "Promise(" => Token::Promise,
        "Default(" => Token::Deflt,
        "Contract(" => Token::Contract,
        "ContractDefault(" => Token::ContractDeflt,
        "Docstring(" => Token::Docstring,

        "isZero" => Token::IsZero,
        "isNum" => Token::IsNum,
        "isBool" => Token::IsBool,
        "isStr" => Token::IsStr,
        "isFun" => Token::IsFun,
        "isList" => Token::IsList,
        "isRecord" => Token::IsRecord,
        "blame" => Token::Blame,
        "chngPol" => Token::ChangePol,
        "polarity" => Token::Polarity,
        "goDom" => Token::GoDom,
        "goCodom" => Token::GoCodom,
        "wrap" => Token::Wrap,
        "unwrap" => Token::Unwrap,
        "embed" => Token::Embed,
        "mapRec" => Token::MapRec,
        "seq" => Token::Seq,
        "deepSeq" => Token::DeepSeq,
        "head" => Token::Head,
        "tail" => Token::Tail,
        "length" => Token::Length,
        "fieldsOf" => Token::FieldsOf,

        "hasField" => Token::HasField,
        "map" => Token::Map,
        "elemAt" => Token::ElemAt,
        "merge" => Token::Merge,

        "{" => Token::LBrace,
        "}" => Token::RBrace,
        "[" => Token::LBracket,
        "]" => Token::RBracket,
        "(" => Token::LParen,
        ")" => Token::RParen,
        "<" => Token::LAngleBracket,
        ">" => Token::RAngleBracket,
    }
}
