use crate::identifier::Ident;
use crate::term::{BinaryOp, RichTerm, Term, UnaryOp, StrChunk, MetaValue,
    MergePriority};
use crate::term::make as mk_term;
use crate::mk_app;
use crate::types::{Types, AbsType};
use super::utils::{StringKind, mk_span, mk_label, strip_indent, SwitchCase,
    strip_indent_doc};
use std::ffi::OsString;
use super::lexer::{Token, NormalToken, StringToken, MultiStringToken, LexicalError};
use std::collections::HashMap;
use std::cmp::min;
use either::*;
use codespan::FileId;

grammar<'input>(src_id: FileId, );

SpTerm<Rule>: RichTerm =
    <l: @L> <t: Rule> <r: @R> => match t {
        RichTerm {term: t, pos: _} => RichTerm {
            term: t,
            pos: Some(mk_span(src_id, l, r))
        }
    };

TypeAnnot: (usize, Types, usize) = ":" <l: @L> <ty: Types> <r: @R> => (l, ty, r);

MetaAnnotAtom: MetaValue = {
    "|" <l: @L> <ty: Types> <r: @R> => MetaValue {
        doc: None,
        contract: Some((ty.clone(), mk_label(ty, src_id, l, r))),
        priority: Default::default(),
        value: None,
    },
    "|" "default" => MetaValue {
        doc: None,
        contract: None,
        priority: MergePriority::Default,
        value: None,
    },
    "|" "doc" <s: Str> => MetaValue {
        doc: Some(strip_indent_doc(s)),
        contract: None,
        priority: Default::default(),
        value: None,
    },
};

MetaAnnot: MetaValue =
    <anns: MetaAnnotAtom+> => {
        anns.into_iter().fold(MetaValue::new(), |acc, meta| {
            MetaValue {
                doc: meta.doc.or(acc.doc),
                contract: meta.contract.or(acc.contract),
                priority: min(acc.priority, meta.priority),
                value: meta.value.or(acc.value),
            }
        })
    };

LeftOp<Op, Current, Previous>: RichTerm =
    <t1: Current> <op: Op> <t2: Previous> => mk_term::op2(op, t1,
    t2);

LeftOpLazy<Op, Current, Previous>: RichTerm =
    <t1: Current> <op: Op> <t2: Previous> => mk_app!(Term::Op1(op, t1), t2);

pub Term: RichTerm = {
    SpTerm<RichTerm>,
    SpTerm<TypedTerm>,
    SpTerm<MetaTerm>,
};

pub ToplevelLet: (Ident, RichTerm) = {
    "let" <id:Ident> <ann: TypeAnnot?> "=" <t1: Term> => {
        let t1 = if let Some((l, ty, r)) = ann {
            let pos = t1.pos.clone();
            RichTerm::new(Term::Promise(ty.clone(), mk_label(ty, src_id, l, r), t1), pos)
        }
        else {
            t1
        };

        (id, t1)
    },
}

TypedTerm: RichTerm = {
    <t: SpTerm<RichTerm>> <ann: TypeAnnot> => {
        let (l, ty, r) = ann;
        RichTerm::from(Term::Promise(ty.clone(), mk_label(ty, src_id, l, r), t))
    }
};

MetaTerm: RichTerm = {
    <t: SpTerm<RichTerm>> <meta: MetaAnnot> => {
        let pos = t.pos.clone();
        //TODO: bump LALRPOP version to >= 0.18.0 which allows mutable x in
        //actions and remove this
        let mut meta = meta;
        meta.value = Some(t);
        RichTerm::new(Term::MetaValue(meta), pos)
    }
};

RichTerm: RichTerm = {
    <l: @L> "fun" <ps:Pattern+> "=>" <t: SpTerm<RichTerm>> <r: @R> => {
        let pos = Some(mk_span(src_id, l, r));
        ps.into_iter().rev().fold(t, |t, p| RichTerm {
            term: Box::new(Term::Fun(p, t)),
            pos: pos.clone()
        })
    },
    "let" <id:Ident> <ann: TypeAnnot?> "=" <t1: Term> "in" <t2:SpTerm<RichTerm>> => {
        let t1 = if let Some((l, ty, r)) = ann {
            let pos = t1.pos.clone();
            RichTerm::new(Term::Promise(ty.clone(), mk_label(ty, src_id, l, r), t1), pos)
        }
        else {
            t1
        };

        mk_term::let_in(id, t1, t2)
    },
    "let" <id:Ident> <meta: MetaAnnot> "=" <t1: Term> "in" <t2:SpTerm<RichTerm>> => {
        //TODO: bump LALRPOP version to >= 0.18.0 which allows mutable x in
        //actions and remove this
        let mut meta = meta;
        meta.value = Some(t1);
        mk_term::let_in(id, Term::MetaValue(meta), t2)
    },
    "switch" "{" <cases: (switch_case ",")*> <last: switch_case?> "}" <exp: SpTerm<RichTerm>> => {
        let mut acc = HashMap::with_capacity(cases.len());
        let mut default = None;

        for case in cases.into_iter().map(|x| x.0).chain(last.into_iter()) {
            match case {
                SwitchCase::Normal(id, t) => acc.insert(id, t),
                // If there are multiple default cases, the last one silently
                // erases the others. We should have a dedicated error for that
                SwitchCase::Default(t) => default.replace(t),
            };
        }

        RichTerm::from(
            Term::Switch(
                exp,
                acc,
                default,
            )
        )
    },
    "if" <b:Term> "then" <t:Term> "else" <e:SpTerm<RichTerm>> =>
        mk_app!(Term::Op1(UnaryOp::Ite(), b), t, e),
    "import" <s: Str> => RichTerm::from(Term::Import(OsString::from(s))),
    SpTerm<InfixExpr>,
};

Applicative: RichTerm = {
    <t1:SpTerm<Applicative>> <t2: SpTerm<Atom>> => mk_app!(t1, t2),
    <op: UOp> <t: SpTerm<Atom>> => mk_term::op1(op, t),
    <op: BOpPre> <t1: SpTerm<Atom>> <t2: SpTerm<Atom>> => mk_term::op2(op, t1, t2),
    SpTerm<RecordOperationChain>,
    SpTerm<Atom>,
};

RecordOperand: RichTerm = {
    SpTerm<Atom>,
    SpTerm<RecordOperationChain>,
}

RecordOperationChain: RichTerm = {
    <t: SpTerm<RecordOperand>> "." <id: Ident> => mk_term::op1(UnaryOp::StaticAccess(id), t),
    <t: SpTerm<RecordOperand>> ".$" <t_id: SpTerm<Atom>> => mk_term::op2(BinaryOp::DynAccess(), t_id, t),
    <t: SpTerm<RecordOperand>> "-$" <t_id: SpTerm<Atom>> => mk_term::op2(BinaryOp::DynRemove(), t_id, t),
    <r: SpTerm<RecordOperand>> "$[" <id: Term> "=" <t: Term> "]" =>
        mk_app!(mk_term::op2(BinaryOp::DynExtend(), id, r), t),
};

Atom: RichTerm = {
    "(" <Term> ")",
    <l: @L> "Assume(" <ty: Types> "," <t: Term> ")" <r: @R> =>
        RichTerm::from(Term::Assume(ty.clone(), mk_label(ty, src_id, l, r), t)),
    <l: @L> "Contract(" <ty: Types> ")" <r: @R> =>
        RichTerm::from(
            Term::MetaValue(
                MetaValue {
                    doc: None,
                    contract: Some((ty.clone(), mk_label(ty, src_id, l, r))),
                    priority: Default::default(),
                    value: None,
                }
            )
        ),
    "Default(" <t: Term> ")" =>
        RichTerm::from(
            Term::MetaValue(
                MetaValue {
                    doc: None,
                    contract: None,
                    priority: MergePriority::Default,
                    value: Some(t),
                }
            )
        ),
    <l: @L> "ContractDefault(" <ty: Types> "," <t: Term> ")" <r: @R> =>
        RichTerm::from(
            Term::MetaValue(
                MetaValue {
                    doc: None,
                    contract: Some((ty.clone(), mk_label(ty, src_id, l, r))),
                    priority: MergePriority::Default,
                    value: Some(t),
                }
            )
        ),
    "Docstring(" <s: Str> "," <t: Term> ")" =>
        RichTerm::from(
            Term::MetaValue(
                MetaValue {
                    doc: Some(s),
                    contract: None,
                    priority: Default::default(),
                    value: Some(t)
                }
            )
        ),
    "num literal" => RichTerm::from(Term::Num(<>)),
    Bool => RichTerm::from(Term::Bool(<>)),
    <StrChunks>,
    Ident => RichTerm::from(Term::Var(<>)),
    "`" <Ident> => RichTerm::from(Term::Enum(<>)),
    "{" <fields: (RecordField ";")*> <last: RecordField?> "}" => {
        let mut static_map = HashMap::new();
        let mut dynamic_fields = Vec::new();

        fields
            .into_iter()
            .map(|x| x.0)
            .chain(last.into_iter())
            .for_each(|field| match field {
                Left((id, t)) => { static_map.insert(id, t) ;}
                Right(t) => dynamic_fields.push(t),
            });

        let static_rec = RichTerm::from(Term::RecRecord(static_map));

        dynamic_fields.into_iter().fold(static_rec, |rec, field| {
            let (id_t, t) = field;
            mk_app!(mk_term::op2(BinaryOp::DynExtend(), id_t, rec), t)
        })
    },
    "[" <terms: (SpTerm<Atom> ",")*> <last: Term?> "]" => {
        let terms : Vec<RichTerm> = terms.into_iter()
            .map(|x| x.0)
            .chain(last.into_iter()).collect();
        RichTerm::from(Term::List(terms))
    }
};

RecordField: Either<(Ident, RichTerm), (RichTerm, RichTerm)> = {
    <id: Ident> <ann: TypeAnnot?> "=" <t: Term> => {
        let t = if let Some((l, ty, r)) = ann {
            let pos = t.pos.clone();
            RichTerm::new(Term::Promise(ty.clone(), mk_label(ty, src_id, l, r), t), pos)
        }
        else {
            t
        };

        Either::Left((id, t))
    },
    "$" <id: SpTerm<Atom>> <ann: TypeAnnot?> "=" <t: Term> => {
        let t = if let Some((l, ty, r)) = ann {
            let pos = t.pos.clone();
            RichTerm::new(Term::Promise(ty.clone(), mk_label(ty, src_id, l, r), t), pos)
        }
        else {
            t
        };

        Either::Right((id, t))
    }
}

Pattern: Ident = {
    Ident,
};

Ident: Ident = "identifier" => Ident(<>.to_string());

Bool: bool = {
    "true" => true,
    "false" => false,
};

StrChunks : RichTerm = {
  <start: StringStart> <fst: ChunkLiteral?> <chunks: (ChunkExpr+ChunkLiteral)*> <lasts:
    ChunkExpr*> <end: StringEnd> => {
        assert_eq!(start, end);

        let chunks: Vec<StrChunk<RichTerm>> = fst.into_iter()
            .map(StrChunk::Literal)
            .chain(chunks.into_iter()
                .map(|(mut es, s)| {
                    es.push(StrChunk::Literal(s));
                    es
                })
                .flatten())
            .chain(lasts.into_iter())
            .collect();

        let mut chunks = if start == StringKind::Multiline {
            strip_indent(chunks)
        }
        else {
            chunks
        };
        chunks.reverse();

        RichTerm::from(Term::StrChunks(chunks))
    },
};

StringStart : StringKind = {
    "\"" => StringKind::Standard,
    "m#\"" => StringKind::Multiline,
};

StringEnd : StringKind = {
    "\"" => StringKind::Standard,
    "\"#m" => StringKind::Multiline,
};

ChunkLiteral : String =
    <parts: ChunkLiteralPart+> => {
        parts.into_iter().fold(String::new(), |mut acc, part| {
            match part {
                Either::Left(s) => acc.push_str(s),
                Either::Right(c) => acc.push(c),
            };

            acc
        })
    };

ChunkExpr: StrChunk<RichTerm> = HashBrace <t: SpTerm<RichTerm>> "}" => StrChunk::Expr(t, 0);

HashBrace = { "#{", "multstr #{" };

Str: String = "\"" <s: ChunkLiteral> "\"" => s;

ChunkLiteralPart: Either<&'input str, char> = {
    "str literal" => Either::Left(<>),
    "str #" => Either::Left(<>),
    "multstr literal" => Either::Left(<>),
    "false interpolation" => Either::Left(<>),
    "false end" => Either::Left(<>),
    "str esc char" => Either::Right(<>),
    };

UOp: UnaryOp = {
    "isNum" => UnaryOp::IsNum(),
    "isBool" => UnaryOp::IsBool(),
    "isStr" => UnaryOp::IsStr(),
    "isFun" => UnaryOp::IsFun(),
    "isList" => UnaryOp::IsList(),
    "isRecord" => UnaryOp::IsRecord(),
    "blame" => UnaryOp::Blame(),
    "chngPol" => UnaryOp::ChangePolarity(),
    "polarity" => UnaryOp::Pol(),
    "goDom" => UnaryOp::GoDom(),
    "goCodom" => UnaryOp::GoCodom(),
    "wrap" => UnaryOp::Wrap(),
    "embed" <Ident> => UnaryOp::Embed(<>),
    "map"  => UnaryOp::ListMap(),
    "recordMap" => UnaryOp::RecordMap(),
    "seq" => UnaryOp::Seq(),
    "deepSeq" => UnaryOp::DeepSeq(),
    "head" => UnaryOp::ListHead(),
    "tail" => UnaryOp::ListTail(),
    "length" => UnaryOp::ListLength(),
    "fieldsOf" => UnaryOp::FieldsOf(),
};

switch_case: SwitchCase = {
    <id: Ident> "=>" <t: Term> => SwitchCase::Normal(id, t),
    "_" "=>" <t: Term> => SwitchCase::Default(<>),
}

// TODO: convenience for messing with precedence levels during development. Once
// operators are fixed, we can inline `InfixExpr0` into `InfixExpr1`
InfixExpr0: RichTerm = {
    Applicative,
}

PrefixExpr1: RichTerm = {
    InfixExpr0,
    "-" <t: PrefixExpr1> =>
        mk_term::op2(BinaryOp::Sub(), Term::Num(0.0), t),
}

BinOp2: BinaryOp = {
    "++" => BinaryOp::PlusStr(),
    "@" => BinaryOp::ListConcat(),
}

InfixExpr2: RichTerm = {
    PrefixExpr1,
    LeftOp<BinOp2, InfixExpr2, PrefixExpr1> => <>,
}

BinOp3: BinaryOp = {
    "*" => BinaryOp::Mult(),
    "/" => BinaryOp::Div(),
    "%" => BinaryOp::Modulo(),
}

InfixExpr3: RichTerm = {
    InfixExpr2,
    LeftOp<BinOp3, InfixExpr3, InfixExpr2> => <>,
}

BinOp4: BinaryOp = {
    "+" => BinaryOp::Plus(),
    "-" => BinaryOp::Sub(),
}

InfixExpr4: RichTerm = {
    InfixExpr3,
    LeftOp<BinOp4, InfixExpr4, InfixExpr3> => <>,
}

PrefixExpr5: RichTerm = {
    InfixExpr4,
    "!" <PrefixExpr5> => mk_term::op1(UnaryOp::BoolNot(), <>),
}

BinOp6: BinaryOp = {
    "&" => BinaryOp::Merge(),
}

PrefixExpr6: RichTerm = {
    PrefixExpr5,
    LeftOp<BinOp6, PrefixExpr6, PrefixExpr5> => <>,
}

BinOp7: BinaryOp = {
    "<" => BinaryOp::LessThan(),
    "<=" => BinaryOp::LessOrEq(),
    ">" => BinaryOp::GreaterThan(),
    ">=" => BinaryOp::GreaterOrEq(),
}

InfixExpr7: RichTerm = {
    PrefixExpr6,
    LeftOp<BinOp7, InfixExpr7, PrefixExpr6> => <>,
}

BinOp8: BinaryOp = {
    "==" => BinaryOp::Eq(),
}

InfixExpr8: RichTerm = {
    InfixExpr7,
    LeftOp<BinOp8, InfixExpr8, InfixExpr7> => <>,
}

LazyBinOp9: UnaryOp = {
    "&&" => UnaryOp::BoolAnd(),
}

InfixExpr9: RichTerm = {
    InfixExpr8,
    LeftOpLazy<LazyBinOp9, InfixExpr9, InfixExpr8> => <>
}

LazyBinOp10: UnaryOp = {
    "||" => UnaryOp::BoolOr(),
}

InfixExpr10: RichTerm = {
    InfixExpr9,
    LeftOpLazy<LazyBinOp10, InfixExpr10, InfixExpr9> => <>
}

// TODO: convenience for adding precedence levels during development. Once
// operators are fixed, we should turn the last level into `InfixExpr` directly
InfixExpr: RichTerm = {
    InfixExpr10,
}

BOpPre: BinaryOp = {
    "unwrap" => BinaryOp::Unwrap(),
    "goField" => BinaryOp::GoField(),
    "hasField" => BinaryOp::HasField(),
    "elemAt" => BinaryOp::ListElemAt(),
    "tag" => BinaryOp::Tag(),
}

Types: Types = {
    "forall" <ids: Ident+> "." <ty: Arrows> =>
        ids.into_iter().rev().fold(
            ty,
            |acc, id| Types(AbsType::Forall(id, Box::new(acc)))
        ),
    <Arrows>
}

Arrows: Types = {
    <s: subType> "->" <t:Arrows> => Types(AbsType::Arrow(Box::new(s), Box::new(t))),
    <subType>,
};

BaseType: Types = {
    "Dyn" => Types(AbsType::Dyn()),
    "Num" => Types(AbsType::Num()),
    "Bool" => Types(AbsType::Bool()),
    "Str" => Types(AbsType::Str()),
    "List" => Types(AbsType::List()),
};

RowTail: Types = {
    <Ident> => Types(AbsType::Var(<>)),
    "Dyn" => Types(AbsType::Dyn()),
}

subType : Types = {
    <BaseType>,
    <Ident> => Types(AbsType::Var(<>)),
    "#" <SpTerm<Atom>> => Types(AbsType::Flat(<>)),
    "(" <Types> ")" => <>,
    "<" <rows:(<Ident> ",")*> <last: (<Ident>)?> <tail: ("|" <Ident>)?> ">" => {
        let ty = rows.into_iter()
            .chain(last.into_iter())
            // As we build row types as a linked list via a fold on the original
            // iterator, the order of identifiers is reversed. This not a big deal
            // but it's less confusing to the user to print them in the original
            // order for error reporting.
            .rev()
            .fold(
                Types(
                    match tail {
                        Some(id) => AbsType::Var(id),
                        None => AbsType::RowEmpty(),
                    }
                ),
                |t, i| Types(AbsType::RowExtend(i, None, Box::new(t)))
            );
        Types(AbsType::Enum(Box::new(ty)))
    },
    "{" <rows:(<Ident> ":" <Types> ",")*>
        <last:(<Ident> ":" <Types>)?>
        <tail: ("|" <RowTail>)?> "}" => {
        let ty = rows.into_iter()
            .chain(last.into_iter())
            // As we build row types as a linked list via a fold on the original
            // iterator, the order of identifiers is reversed. This not a big deal
            // but it's less confusing to the user to print them in the original
            // order for error reporting.
            .rev()
            .fold(
                tail.unwrap_or(Types(AbsType::RowEmpty())),
                |t, i_ty| {
                    let (i, ty) = i_ty;
                    Types(AbsType::RowExtend(i, Some(Box::new(ty)), Box::new(t)))
                }
            );
        Types(AbsType::StaticRecord(Box::new(ty)))
    },
    "{" "_" ":" <Types> "}" => Types(AbsType::DynRecord(Box::new(<>))),
};

extern {
    type Location = usize;
    type Error = LexicalError;

    enum Token<'input> {
        "identifier" => Token::Normal(NormalToken::Identifier(<&'input str>)),
        "str literal" => Token::Str(StringToken::Literal(<&'input str>)),
        "str #" => Token::Str(StringToken::Hash(<&'input str>)),
        "str esc char" => Token::Str(StringToken::EscapedChar(<char>)),
        "multstr literal" => Token::MultiStr(MultiStringToken::Literal(<&'input str>)),
        "false end" => Token::MultiStr(MultiStringToken::FalseEnd(<&'input str>)),
        "false interpolation" => Token::MultiStr(MultiStringToken::FalseInterpolation(<&'input str>)),
        "num literal" => Token::Normal(NormalToken::NumLiteral(<f64>)),

        "if" => Token::Normal(NormalToken::If),
        "then" => Token::Normal(NormalToken::Then),
        "else" => Token::Normal(NormalToken::Else),
        "forall" => Token::Normal(NormalToken::Forall),
        "in" => Token::Normal(NormalToken::In),
        "let" => Token::Normal(NormalToken::Let),
        "switch" => Token::Normal(NormalToken::Switch),

        "true" => Token::Normal(NormalToken::True),
        "false" => Token::Normal(NormalToken::False),

        "," => Token::Normal(NormalToken::Comma),
        ":" => Token::Normal(NormalToken::Colon),
        "$" => Token::Normal(NormalToken::Dollar),
        "=" => Token::Normal(NormalToken::Equals),
        ";" => Token::Normal(NormalToken::SemiCol),
        "&" => Token::Normal(NormalToken::Ampersand),
        "." => Token::Normal(NormalToken::Dot),
        ".$" => Token::Normal(NormalToken::DotDollar),
        "$[" => Token::Normal(NormalToken::DollarBracket),
        "#{" => Token::Str(StringToken::HashBrace),
        "multstr #{" => Token::MultiStr(MultiStringToken::Interpolation),
        "-$" => Token::Normal(NormalToken::MinusDollar),

        "+" => Token::Normal(NormalToken::Plus),
        "-" => Token::Normal(NormalToken::Minus),
        "*" => Token::Normal(NormalToken::Times),
        "/" => Token::Normal(NormalToken::Div),
        "%" => Token::Normal(NormalToken::Percent),
        "++" => Token::Normal(NormalToken::DoublePlus),
        "==" => Token::Normal(NormalToken::DoubleEq),
        "@" => Token::Normal(NormalToken::At),
        "&&" => Token::Normal(NormalToken::DoubleAnd),
        "||" => Token::Normal(NormalToken::DoublePipe),
        "!" => Token::Normal(NormalToken::Bang),

        "$=" => Token::Normal(NormalToken::DollarEquals),
        "fun" => Token::Normal(NormalToken::Fun),
        "import" => Token::Normal(NormalToken::Import),
        "|" => Token::Normal(NormalToken::Pipe),
        "->" => Token::Normal(NormalToken::SimpleArrow),
        "=>" => Token::Normal(NormalToken::DoubleArrow),
        "#" => Token::Normal(NormalToken::Hash),
        "`" => Token::Normal(NormalToken::Backtick),
        "_" => Token::Normal(NormalToken::Underscore),
        "\"" => Token::Normal(NormalToken::DoubleQuote),
        "\"#m" => Token::MultiStr(MultiStringToken::End),
        "m#\"" => Token::Normal(NormalToken::MultiStringStart(<usize>)),

        "Num" => Token::Normal(NormalToken::Num),
        "Dyn" => Token::Normal(NormalToken::Dyn),
        "Str" => Token::Normal(NormalToken::Str),
        "Bool" => Token::Normal(NormalToken::Bool),
        "List" => Token::Normal(NormalToken::List),

        "tag" => Token::Normal(NormalToken::Tag),
        "Assume(" => Token::Normal(NormalToken::Assume),
        "Promise(" => Token::Normal(NormalToken::Promise),
        "Default(" => Token::Normal(NormalToken::Deflt),
        "Contract(" => Token::Normal(NormalToken::Contract),
        "ContractDefault(" => Token::Normal(NormalToken::ContractDeflt),
        "Docstring(" => Token::Normal(NormalToken::Docstring),
        "isNum" => Token::Normal(NormalToken::IsNum),
        "isBool" => Token::Normal(NormalToken::IsBool),
        "isStr" => Token::Normal(NormalToken::IsStr),
        "isFun" => Token::Normal(NormalToken::IsFun),
        "isList" => Token::Normal(NormalToken::IsList),
        "isRecord" => Token::Normal(NormalToken::IsRecord),
        "blame" => Token::Normal(NormalToken::Blame),
        "chngPol" => Token::Normal(NormalToken::ChangePol),
        "polarity" => Token::Normal(NormalToken::Polarity),
        "goDom" => Token::Normal(NormalToken::GoDom),
        "goCodom" => Token::Normal(NormalToken::GoCodom),
        "goField" => Token::Normal(NormalToken::GoField),
        "wrap" => Token::Normal(NormalToken::Wrap),
        "unwrap" => Token::Normal(NormalToken::Unwrap),
        "embed" => Token::Normal(NormalToken::Embed),
        "recordMap" => Token::Normal(NormalToken::RecordMap),
        "seq" => Token::Normal(NormalToken::Seq),
        "deepSeq" => Token::Normal(NormalToken::DeepSeq),
        "head" => Token::Normal(NormalToken::Head),
        "tail" => Token::Normal(NormalToken::Tail),
        "length" => Token::Normal(NormalToken::Length),
        "fieldsOf" => Token::Normal(NormalToken::FieldsOf),

        "hasField" => Token::Normal(NormalToken::HasField),
        "map" => Token::Normal(NormalToken::Map),
        "elemAt" => Token::Normal(NormalToken::ElemAt),
        "merge" => Token::Normal(NormalToken::Merge),
        "default" => Token::Normal(NormalToken::Default),
        "doc" => Token::Normal(NormalToken::Doc),

        "{" => Token::Normal(NormalToken::LBrace),
        "}" => Token::Normal(NormalToken::RBrace),
        "[" => Token::Normal(NormalToken::LBracket),
        "]" => Token::Normal(NormalToken::RBracket),
        "(" => Token::Normal(NormalToken::LParen),
        ")" => Token::Normal(NormalToken::RParen),
        "<" => Token::Normal(NormalToken::LAngleBracket),
        "<=" => Token::Normal(NormalToken::LessOrEq),
        ">" => Token::Normal(NormalToken::RAngleBracket),
        ">=" => Token::Normal(NormalToken::GreaterOrEq),
    }
}
